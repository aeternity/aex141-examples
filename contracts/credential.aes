// Copyright (c) 2022 Toqns Inc and contributors.

// ISC License

// Permission to use, copy, modify, and/or distribute this software for any
// purpose with or without fee is hereby granted, provided that the above
// copyright notice and this permission notice appear in all copies.

// THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
// REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
// INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
// LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
// OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
// PERFORMANCE OF THIS SOFTWARE.

// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST

@compiler >= 6

include "List.aes"
include "Option.aes"
include "String.aes"
include "core/utils.aes"
include "core/interfaces.aes"

// CredentialNFT implements the mintable and burnable extension.
// The example demonstrates gradiation credentials issued as an NFT.
contract CredentialNFT =
    // Events.
    // Transfer(from, to, token_id)
    // Approval(owner, approved, token_id, enabled)
    // ApprovalForAll(owner, operator, enabled)
    datatype event 
        = Transfer(address, address, int)
        | Approval(address, address, int, string)
        | ApprovalForAll(address, address, string)

    // Metadata has a type and value.
    //
    // Types:
    //
    // URL is any url except IPFS.
    // IPFS is an IPFS ID.
    // OBJECT_ID is a unique identifier for a physical 
    // object (car's VIN number, real estate cadastral code, etc)
    // MAP is for other key/value based properties. 
    //
    // Values:
    // URL, IPFD, OBJECT_ID: string
    // MAP: map(string, string)
    datatype metadata_type = URL | IPFS | OBJECT_ID | MAP
    datatype metadata = String(string) | Map(string, string)

    record meta_info = 
        { name: string
        , symbol: string
        , metadata_type : metadata_type 
        , base_url: option(string) }
    
    record state = 
        { owner: address
        , meta_info: meta_info
        , owners: map(int, address)
        , balances: map(address, int)
        , approvals: map(int, address)
        , operators: map(address, map(address, bool))
        , metadata: map(int, metadata)
        , counter: int }

    // Constructor. During initialization the contract's meta data will
    // be set.
    stateful entrypoint init(name: string, symbol: string) = 
        require(String.length(name) >= 1, "STRING_TOO_SHORT_NAME")
        require(String.length(symbol) >= 1, "STRING_TOO_SHORT_SYMBOL")

        { owner = Call.caller,
          meta_info = { name = name, symbol = symbol, metadata_type = MAP, base_url = None },
          owners = {},
          balances = {},
          approvals = {},
          operators = {},
          metadata = {},
          counter = 0 }

    // Issues a new token to the provided address. If the owner is a contract, NFTReceiver will be called 
    // with data if provided.
    //
    // Throws if the call to NFTReceiver implementation failed (safe transfer).
    // 
    // Emits a Transfer event.     
    stateful entrypoint mint(owner: address, metadata: option(metadata), data: option(string)) : int =
        // In this example only the contract owner can mint and contracts can't own credentials. 
        require_contract_owner() 
        require_not_contract(owner)

        switch(metadata)
            None =>
                abort("NO_METADATA_PROVIDED") 
            Some(v) => 
                let token_id = state.counter
                put( state { counter = state.counter + 1, balances[owner = 0] @ b = b + 1, owners[token_id] = owner, metadata[token_id] = v } )
                Chain.event(Transfer(Contract.address, owner, token_id))
                token_id

    // Returns a hardcoded list of all implemented extensions on the deployed contract.
    entrypoint aex141_extensions() : list(string) =
        ["mintable"]

    // Returns meta information associated with the token contract.
    entrypoint meta_info() : meta_info = 
        state.meta_info

    // Returns meta data associated with a token. 
    entrypoint metadata(token_id: int) : option(metadata) = 
        Map.lookup(token_id, state.metadata)

    // Returns the balance of an account with address owner, if the account exists. 
    // If the owner address is unknown to the contract None will be returned. Using option type 
    // as a return value allows us to determine if the account has balance of 0, more than 0, 
    // or the account has never had balance and is still unknown to the contract.
    entrypoint balance(owner: address) : option(int) =
        Map.lookup(owner, state.balances)

    // Returns the owner's address for the provided token, if the token is minted. 
    // If the token isn't minted None will be returned.
    entrypoint owner(token_id: int) : option(address) =
        Map.lookup(token_id, state.owners)

    // Transfers token from the from address to the to address. Will invoke NFTReceiver if to is a contract receiver. 
    // If provided data will be submitted with the invocation of NFTReceiver.
    //
    // Throws if:
    // - Call.caller is not the current owner, an authorized operator or the approved address for this token;
    // - from isn't the current owner;
    // - token isn't a valid token;
    // - the invocation of NFTReceiver fails.
    //
    // Emits the Transfer event.
    stateful entrypoint transfer(from: address, to: address, token_id: int, data: option(string)) : unit =
        // Since graduation credentials should normally not be transfered this function is limited to
        // the contract owner or an address approved by the contract owner to deal with exceptional cases.
        // In this example only physical people can receive a credential, so we exclude transfers to 
        // contracts.
        require_not_contract(to)
        require_contract_owner_or_approved(token_id)
        require_token_owner(token_id, from)
        
        remove_approval(token_id)
        put( state { balances[from] @b = b - 1, balances[to = 0] @nb = nb + 1, owners[token_id] = to } )
        Chain.event(Transfer(from, to, token_id))

    // Sets the approved address to interact on behalf of an owner for the token. 
    // If enabled is true the operator is approved, if false the approval is revoked. 
    //
    // Throws unless caller is the current NFT owner, or an authorized operator of the current owner. 
    //
    // Emits the Approval event.
    stateful entrypoint approve(approved: address, token_id: int, enabled: bool) : unit =
        // Since graduation credentials should not be transfered this function is limited to
        // only the contract owner can give approval.
        require_contract_owner()

        if(enabled)
            put( state { approvals[token_id] = approved })
        else
            remove_approval(token_id)
        Chain.event(Approval(Call.caller, approved, token_id, Utils.bool_to_string(enabled)))


    // Enables or disables approval for an operator to manage all of the caller's assets. 
    // If enabled is true the operator is approved, if false the approval is revoked. 
    //
    // Emits the ApprovalForAll event.
    stateful entrypoint approve_all(operator: address, enabled: bool) : unit =
        // Approve_All should not be used for this kind of NFT.
        abort("NOT_IMPLEMENTED")

    // Returns the address approved to interact with the token or returns None 
    // if no approval has been set. 
    entrypoint get_approved(token_id: int) : option(address) =
        Map.lookup(token_id, state.approvals)

    // Returns true if a is approved to transact for the token.
    entrypoint is_approved(token_id: int, a: address) : bool =
        switch(Map.lookup(token_id, state.approvals))
            None => false
            Some(o) => o == a

    // Returns true if operator is approved to commit transactions on behalf of owner.
    // Indicates wether an address is an authorized operator for another address.
    entrypoint is_approved_for_all(owner: address, operator: address) : bool =
        switch(Map.lookup(owner, state.operators))
            None => false
            Some(ops) =>
                switch(Map.lookup(operator, ops))
                    None => false
                    Some(v) => v

    // Helper functions

    // Returns true if a is the owner of the token with token_id.
    private function is_token_owner(token_id: int, a: address) : bool =
        switch(Map.lookup(token_id, state.owners))
            None => false
            Some(o) => o == a

    // Throws if the caller is not the contract's owners.
    private function require_contract_owner() : unit =
        require(Call.caller == state.owner, "ONLY_CONTRACT_OWNER_CALL_ALLOWED")

    // Throws if the caller is not the contract's owners.
    private function require_contract_owner_or_approved(token_id: int) : unit =
        require(Call.caller == state.owner || is_approved(token_id, Call.caller), "ONLY_CONTRACT_OWNER_OR_APPROVED_CALL_ALLOWED")

    // Throws if a is not the token's owner.
    private function require_token_owner(token_id: int, a: address) : unit =
        require(is_token_owner(token_id, a), "ONLY_OWNER_CALL_ALLOWED")

    // Helper function to remove an approval from the map.
    private stateful function remove_approval(token_id: int) : unit =
        if(Map.member(token_id, state.approvals))
            put( state { approvals = Map.delete(token_id, state.approvals)})
    
    private function require_not_contract(to: address) = 
        require(!Address.is_contract(to), "RECEIVER_CAN_NOT_BE_CONTRACT")
